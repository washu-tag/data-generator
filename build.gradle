plugins {
    id 'groovy'
    id 'java'
}

group = 'edu.washu.tag'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        url 'https://maven.dcm4che.org/'
    }
    maven {
        url 'https://raw.github.com/nroduit/mvn-repo/master/' // needed for dcm4che
    }
}

final String vGroovy = '5.0.0-alpha-11'
final String vJackson = '2.18.0'
final String vHapiHl7V2 = '2.5.1'
final String vCommonsIo = '2.17.0'
final String vCommonsLang3 = '3.17.0'
final String vDcm4che = '5.33.0'
final String vAzureOpenai = '1.0.0-beta.12'
final String vSlf4j = '2.0.16'
final String vLog4j2Bridge = '2.24.2'
final String vCommonsMath3 = '3.6.1'
final String vGpars = '1.2.1'
final String vSparkSql = '4.0.0-preview2'
final String vTestNg = '7.9.0'

configurations {
    all*.exclude group: 'ch.qos.logback', module: 'logback-classic'
    all*.exclude group: 'ch.qos.logback', module: 'logback-core'
}

dependencies {
    implementation("org.apache.groovy:groovy:${vGroovy}")
    implementation("com.fasterxml.jackson.core:jackson-databind:${vJackson}")
    implementation("com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:${vJackson}")
    implementation("ca.uhn.hapi:hapi-base:${vHapiHl7V2}")
    implementation("ca.uhn.hapi:hapi-structures-v281:${vHapiHl7V2}")
    implementation("commons-io:commons-io:${vCommonsIo}")
    implementation("org.apache.commons:commons-lang3:${vCommonsLang3}")
    implementation("org.dcm4che.tool:dcm4che-tool-storescu:${vDcm4che}")
    implementation("com.azure:azure-ai-openai:${vAzureOpenai}")
    implementation("org.slf4j:slf4j-api:${vSlf4j}")
    implementation("org.apache.logging.log4j:log4j-slf4j2-impl:${vLog4j2Bridge}")
    implementation("org.apache.commons:commons-math3:${vCommonsMath3}")
    implementation("org.codehaus.gpars:gpars:${vGpars}")
    implementation("org.apache.spark:spark-sql_2.13:${vSparkSql}")
    implementation("io.delta:delta-spark_2.12:3.1.0")
    implementation("org.apache.hadoop:hadoop-aws:3.2.0")
    implementation("org.testng:testng:${vTestNg}")
}

java {
    sourceCompatibility = '21'
}

test {
    useTestNG {

    }
}

tasks.register('try', JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'edu.washu.tag.generator.QueryTester'

    // thanks to https://stackoverflow.com/a/76805763
    jvmArgs = [
        '--add-opens=java.base/java.lang=ALL-UNNAMED',
        '--add-opens=java.base/java.lang.invoke=ALL-UNNAMED',
        '--add-opens=java.base/java.lang.reflect=ALL-UNNAMED',
        '--add-opens=java.base/java.io=ALL-UNNAMED',
        '--add-opens=java.base/java.net=ALL-UNNAMED',
        '--add-opens=java.base/java.nio=ALL-UNNAMED',
        '--add-opens=java.base/java.util=ALL-UNNAMED',
        '--add-opens=java.base/java.util.concurrent=ALL-UNNAMED',
        '--add-opens=java.base/java.util.concurrent.atomic=ALL-UNNAMED',
        '--add-opens=java.base/sun.nio.ch=ALL-UNNAMED',
        '--add-opens=java.base/sun.nio.cs=ALL-UNNAMED',
        '--add-opens=java.base/sun.security.action=ALL-UNNAMED',
        '--add-opens=java.base/sun.util.calendar=ALL-UNNAMED',
        '--add-opens=java.security.jgss/sun.security.krb5=ALL-UNNAMED'
    ]
}

tasks.register('generateSpecs', JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'edu.washu.tag.generator.PopulationGenerator'
    args(
        propertyWithDefault('config', 'default'),
        propertyWithDefault('writeToFiles', false),
        propertyWithDefault('generateTestQueries', false)
    )
}

tasks.register('writeFiles', JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'edu.washu.tag.generator.BatchProcessor'
    final String fullSpec = 'batchSpecs'
    final String shortcut = 'standardSpecs'
    final List<Object> arguments = []
    if (project.hasProperty(fullSpec)) {
        arguments << project.property(fullSpec)
    } else if (project.hasProperty(shortcut)) {
        arguments << (0 .. Integer.parseInt(project.property(shortcut) as String)).collect {
            "batches/batch_${it}.yaml"
        }.join(',')
    }
    arguments << propertyWithDefault('writeFiles', true)
    arguments << propertyWithDefault('generateTestQueries', false)
    args(arguments.toArray())
}

Object propertyWithDefault(String propName, Object defaultVal) {
    project.hasProperty(propName) ? project.property(propName) : defaultVal
}
