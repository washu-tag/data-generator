plugins {
    id 'groovy'
    id 'org.springframework.boot' version '3.4.1'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'java'
}

group = 'edu.washu.tag'
version = '1.0-SNAPSHOT'

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url 'https://maven.dcm4che.org/'
    }
    maven {
        url 'https://raw.github.com/nroduit/mvn-repo/master/' // needed for dcm4che
    }
    maven {
        url 'https://nrgxnat.jfrog.io/nrgxnat/libs-release'
    }
    maven {
        url 'https://nrgxnat.jfrog.io/nrgxnat/libs-snapshot'
    }
}

final String vGroovy = '5.0.0-alpha-11'
final String vQuerySpec = '1.0-SNAPSHOT'
final String vJackson = '2.18.0'
final String vHapiHl7V2 = '2.5.1'
final String vCommonsIo = '2.17.0'
final String vCommonsLang3 = '3.17.0'
final String vDcm4che = '5.33.0'
final String vOpenai = '2.7.0'
final String vCommonsMath3 = '3.6.1'
final String vGpars = '1.2.1'
final String vTestNg = '7.9.0'
final String vLog4j2Bridge = '2.24.2'
final String vTemporal = '1.28.0'

configurations {
    all*.exclude group: 'ch.qos.logback', module: 'logback-classic'
    all*.exclude group: 'ch.qos.logback', module: 'logback-core'

    all {
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
    }
}

dependencies {
    implementation("org.apache.groovy:groovy:${vGroovy}")
    implementation("edu.washu.tag:query-spec:${vQuerySpec}")
    implementation("com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:${vJackson}")
    implementation("com.fasterxml.jackson.datatype:jackson-datatype-jsr310:${vJackson}")
    implementation("ca.uhn.hapi:hapi-base:${vHapiHl7V2}")
    implementation("ca.uhn.hapi:hapi-structures-v281:${vHapiHl7V2}")
    implementation("commons-io:commons-io:${vCommonsIo}")
    implementation("org.apache.commons:commons-lang3:${vCommonsLang3}")
    implementation("org.dcm4che.tool:dcm4che-tool-storescu:${vDcm4che}")
    implementation("com.openai:openai-java:${vOpenai}")
    implementation("org.apache.commons:commons-math3:${vCommonsMath3}")
    implementation("org.codehaus.gpars:gpars:${vGpars}")
    implementation("org.testng:testng:${vTestNg}")
    implementation("org.apache.logging.log4j:log4j-slf4j2-impl:${vLog4j2Bridge}")
    implementation("io.temporal:temporal-sdk:${vTemporal}")
    implementation("io.temporal:temporal-spring-boot-starter:${vTemporal}")
    implementation('org.springframework.boot:spring-boot-starter')
}

java {
    sourceCompatibility = '21'
}

test {
    useTestNG {

    }
}

tasks.register('generateSpecs', JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'edu.washu.tag.generator.PopulationGenerator'
    args(
        propertyWithDefault('config', 'default'),
        propertyWithDefault('writeToFiles', false),
        propertyWithDefault('generateTestQueries', false),
        propertyWithDefault('outputDir', '.')
    )
}

tasks.register('writeFiles', JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'edu.washu.tag.generator.BatchProcessor'
    args(
        resolveBatchArg(),
        propertyWithDefault('writeFiles', true),
        propertyWithDefault('generateTestQueries', false),
        propertyWithDefault('outputDir', '.')
    )
}

tasks.register('queryExport', JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'edu.washu.tag.generator.QueryExporter'

    args(resolveBatchArg())
}

Object propertyWithDefault(String propName, Object defaultVal) {
    project.hasProperty(propName) ? project.property(propName) : defaultVal
}

String resolveBatchArg() {
    final String fullSpec = 'batchSpecs'
    final String shortcut = 'standardSpecs'
    if (project.hasProperty(fullSpec)) {
        project.property(fullSpec)
    } else if (project.hasProperty(shortcut)) {
        (0 .. Integer.parseInt(project.property(shortcut) as String)).collect {
            "batches/batch_${it}.yaml"
        }.join(',')
    } else {
        throw new RuntimeException("-P${fullSpec} or -P${shortcut} must be specified")
    }
}
